
plugins {
  id 'java'
  id 'org.jetbrains.intellij' version '0.4.21'
  id "org.jetbrains.grammarkit" version "2020.2.1"
}


group 'org.c3lang'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

test {

}

// Include the generated files in the source set
sourceSets.main.java.srcDirs 'src/main/gen'

dependencies {
  testImplementation group: 'junit', name: 'junit', version: '4.12'
}

apply plugin: 'org.jetbrains.grammarkit'
import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

task generateC3Lexer(type: GenerateLexer) {
    source "src/main/java/org/c3lang/intellijplugin/C3.flex"
    targetDir "src/main/gen/org/c3lang/intellijplugin/lexer"
    targetClass "C3Lexer"
    purgeOldFiles true
}

task generateC3Parser(type: GenerateParser) {
    source 'src/main/java/org/c3lang/intellijplugin/C3.bnf'
    targetRoot 'src/main/gen'
    pathToParser 'org/c3lang/intellijplugin/parser/C3Parser.java'
    pathToPsiRoot 'org/c3lang/intellijplugin/parser/'
    purgeOldFiles true
}

compileJava {
    dependsOn generateC3Parser
    dependsOn generateC3Lexer
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
  version = '2020.2'
  plugins = ['java']
}

buildSearchableOptions {
  enabled = false
}

patchPluginXml {
  version = project.version
  sinceBuild = '202'
  untilBuild = '202.*'
}